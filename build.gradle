buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.github.townsfolk:gradle-release:1.2'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
	}
}


apply plugin: 'release'
apply plugin: "org.dreamscale.core-oss"
apply plugin: "org.dreamscale.integration-test"
apply plugin: "org.dreamscale.maven-publish-ext"
apply plugin: 'com.jfrog.bintray'

repositories {
	jcenter()
}

dependencies {
	compile gradleApi()
	compile "nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0"

	mainTestCompile 'commons-io:commons-io:2.4'
	mainTestCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
		exclude group: 'org.codehaus.groovy'
	}
	mainTestCompile 'cglib:cglib-nodep:2.2.2'
	mainTestCompile 'org.objenesis:objenesis:1.3'
}

check.dependsOn integrationTest

license {
	ext.name = "DreamScale, Inc"
	skipExistingHeaders true
}

[licenseMain, licenseFormatMain]*.exclude "licenses/*"
// exclude the test kit source which was copied from a different project (https://github.com/alkemist/gradle-test-kit)
// and has it's own license
[licenseMainTest, licenseFormatMainTest]*.exclude "org/gradle/testkit/**"


publishing_ext {
	publication("mainTest")

	publishJavadoc true
	pom {
		packaging "jar"
		description "A collection of gradle plugins meant to codify and streamline project development."
		url "https://github.com/dreamscale-io/gradle-core"
		developers {
			developer {
				id "dreamscale"
				name "DreamScale Open Source Development"
				email "opensource@dreamscale.io"
			}
		}
		scm {
			url "https://github.com/dreamscale-io/gradle-core.git"
			connection "scm:git:git@github.com:dreamscale-io/gradle-core.git"
			developerConnection "scm:git:git@github.com:dreamscale-io/gradle-core.git"
		}
	}
}

bintray {
	user = project.hasProperty('bintray.user') ? project.property('bintray.user') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintray.key') ? project.property('bintray.key') : System.getenv('BINTRAY_API_KEY')

	publications = ['gradleCore', 'gradleCoreTest']
	dryRun = false
	publish = false
	pkg {
		repo = 'maven-public'
		userOrg = 'dreamscale'
		name = project.group + ':' + project.ext['artifactId']
		desc = project.description
		websiteUrl = 'https://github.com/dreamscale-io/gradle-core'
		issueTrackerUrl = 'https://github.com/dreamscale-io/gradle-core/issues'
		vcsUrl = 'https://github.com/dreamscale-io/gradle-core.git'
		licenses = ['Apache-2.0']
		labels = ['gradle']
		publicDownloadNumbers = true
		version {
			vcsTag = project.version
			attributes = ['gradle-plugin': 'org.dreamscale.core-oss:org.dreamscale:gradle-core']
		}
	}
}
